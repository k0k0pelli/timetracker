group = 'id.meier.timetracker'
version = '0.8.0-SNAPSHOT'

buildscript {
	ext {
		springBootVersion = '2.2.0.RELEASE'
	}
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.github.gmazelier:jasperreports-gradle-plugin:0.4"
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.github.gmazelier.jasperreports"

sourceCompatibility = '1.8'

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/"}
}

ext {
	set('vaadinVersion', "14.1.23")
}

dependencies {
	implementation 'com.vaadin:vaadin-spring-boot-starter'

	implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
	implementation group: 'org.springframework.security', name: 'spring-security-web'
	implementation group: 'org.springframework.security', name: 'spring-security-config'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
	implementation group: 'com.h2database', name: 'h2'
	implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.8'
	implementation group: 'org.vaadin.olli', name: 'file-download-wrapper', version: '1.1.0'
	implementation group: 'com.google.guava', name: 'guava', version: '16.0.1'
	implementation group: 'net.sf.jasperreports', name: 'jasperreports', version: '6.12.2'

	testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
	testImplementation('org.springframework.boot:spring-boot-starter-test')
//			{
//		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
//	}
}

dependencyManagement {
	imports {
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

jasperreports {
	srcDir = file('src/main/resources')
	outDir = file("${project.buildDir}/resources/main/id/meier/timetracking/reports")
	srcExt = '.jrxml'
	outExt = '.jasper'
	compiler = 'net.sf.jasperreports.engine.design.JRJdtCompiler'
	keepJava = false
	validateXml = true
	verbose = false
	useRelativeOutDir = false
	classpath = project.sourceSets.main.output
}

compileAllReports.dependsOn compileJava
bootJar.dependsOn compileAllReports
bootJar.dependsOn test

test {
	useJUnitPlatform()
}

task copyScriptsToDistFiles(type: Copy) {
	from 'scripts'
	include '**/*.*'
	into 'dist'
	expand([
			buildNumber: "${version}",
	])
	fileMode 0744
}

task copyJarToDistFiles(type: Copy) {
	from 'build/libs'
	include "**/*.*"
	into 'dist'
}


bootJar.dependsOn copyScriptsToDistFiles
bootJar.dependsOn copyJarToDistFiles
