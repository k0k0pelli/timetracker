
buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/" }
		maven { url "https://plugins.gradle.org/m2/" }
	}

}

plugins {
	id 'com.vaadin' version '0.8.0'
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id "com.github.gmazelier.jasperreports" version "0.4"
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/"}
}

group = 'id.meier.timetracker'
version = '0.9.0-SNAPSHOT'

vaadin {
	pnpmEnable = true
}

ext {
	set('vaadinVersion', "14.3.4")
}

dependencies {
	implementation('com.vaadin:vaadin-spring-boot-starter') {
//         Webjars are only needed when running in Vaadin 13 compatibility mode
		["com.vaadin.webjar", "org.webjars.bowergithub.insites",
		 "org.webjars.bowergithub.polymer", "org.webjars.bowergithub.polymerelements",
		 "org.webjars.bowergithub.vaadin", "org.webjars.bowergithub.webcomponents"]
				.forEach { group -> exclude(group: group) }
	}

	implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
	implementation group: 'org.springframework.security', name: 'spring-security-web'
	implementation group: 'org.springframework.security', name: 'spring-security-config'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
	implementation group: 'com.h2database', name: 'h2'
	implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.8'

	implementation group: 'com.google.guava', name: 'guava', version: '16.0.1'
	implementation group: 'net.sf.jasperreports', name: 'jasperreports', version: '6.12.2'

	testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
	testImplementation('org.springframework.boot:spring-boot-starter-test')
			{
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

dependencyManagement {
	imports {
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

jasperreports {
	srcDir = file('src/main/resources')
	outDir = file("${project.buildDir}/resources/main/id/meier/timetracking/reports")
	srcExt = '.jrxml'
	outExt = '.jasper'
	compiler = 'net.sf.jasperreports.engine.design.JRJdtCompiler'
	keepJava = false
	validateXml = true
	verbose = true
	useRelativeOutDir = false
	classpath = project.sourceSets.main.output
}

compileAllReports.dependsOn compileJava
build.dependsOn compileAllReports
build.dependsOn test

bootRun.dependsOn compileAllReports
bootRun.dependsOn test



test {
	useJUnitPlatform()
}

task copyScriptsToDistFiles(type: Copy) {
	from 'scripts'
	include '**/*.*'
	into 'dist'
	expand([
			buildNumber: "${version}",
	])
	fileMode 0744
}

task copyJarToDistFiles(type: Copy) {
	from 'build/libs'
	include "**/*.*"
	into 'dist'
}


build.dependsOn copyScriptsToDistFiles
build.dependsOn copyJarToDistFiles

bootRun.dependsOn copyScriptsToDistFiles
bootRun.dependsOn copyJarToDistFiles